/**
 * An example MediaMod addon using Kotlin
 *
 * This example uses Kotlin, specifically 1.5.10 at the moment
 * It is not recommended to change the Kotlin version on your own as MediaMod will provide Kotlin at runtime
 * asbyth's ForgeGradle is used here, the official ForgeGradle for 1.8.9 is no longer maintained.
 *
 * This example does not use a Forge Mod, if you want an example that has a Forge Mod and a MediaMod addon
 * You can check out the other examples in this repository
 */

buildscript {
    repositories {
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath "com.github.asbyth:ForgeGradle:da0302a"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'

group 'com.mediamod.example'
version '1.0-SNAPSHOT'
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = true
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    // Don't remove these, they are required by MediaMod! You may need to change the commit hash (03764de at the moment) to a newer hash
    compileOnly "com.google.code.gson:gson:2.8.6"
    compileOnly "com.github.MediaModMC.MediaMod:core:03764de"
    compileOnly "com.github.MediaModMC.MediaMod:forge-1.8.9:03764de"
    compileOnly "gg.essential:Vigilance:107-10809-SNAPSHOT"

    // If you wish to include a dependency in your jar use the "embed" method
    // embed 'com.example:ExampleDependency:1.0-SNAPSHOT'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.embed]
}

tasks.build.finalizedBy tasks.shadowJar
